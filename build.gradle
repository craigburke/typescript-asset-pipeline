plugins {
    id 'groovy'
    id 'com.moowork.node' version '0.10'
    id 'nebula.provided-base' version '2.2.2'
}

group = 'com.craigburke'
version = '0.1.0'

sourceCompatibility = 1.7

repositories {
    mavenLocal()
    jcenter()
}

node {
    download = true
}

ext {
    npmDependencies = ['typescript', 'webpack']

    tscPath = 'node_modules/typescript/lib/'
    tscFile = file("${tscPath}/tsc.js")
    updatedTscFile = file("${tscPath}/tsc.tmp.js")
    tscDestination = file('src/main/resources/tsc.js')
}

task('installCompilerDependencies', type: NpmTask) {
    args = ['install'] + npmDependencies
}

task('processCompiler', dependsOn: 'installCompilerDependencies') {
    doLast {
        String libDTs = file("${tscPath}/lib.d.ts").text
            .replaceAll("'", "\\\\'")
            .replaceAll("\r\n", " \\\\n")

        // Copy files
        copy {
            from 'js'
            into tscPath
        }

        // Update sys-shim
        File sysShim = file("${tscPath}/sys-shim.js")
        sysShim.text = sysShim.text.replace('// Add Files', "files['lib.d.ts'] = '${libDTs}';")

        String updatedJs = "ts.sys = require('./sys-shim');"
        updatedTscFile.text = tscFile.text.replaceFirst(/(?ms)ts\.sys.*\}\)\(\);/, updatedJs)
    }
}

task('installCompiler', dependsOn: 'processCompiler', type: NodeTask) {
    script = file('node_modules/webpack/bin/webpack.js')
    args = [updatedTscFile.path, '--output-library', 'ts', tscDestination.path]
    doFirst {
        String compileFunction = """ \
            module.exports.compileString = function(data) {
                ts.sys.writeFile('foo.ts', data);
                ts.executeCommandLine(['foo.ts']);
                return ts.sys.readFile('foo.js');
            };
        """

        updatedTscFile.text =  updatedTscFile.text.replace('ts.executeCommandLine(ts.sys.args);', compileFunction)
    }
}

dependencies {
    provided 'com.bertramlabs.plugins:asset-pipeline-core:2.0.12'
    compile 'com.craigburke.asset:javascript-core:0.2.1'
    compile 'org.codehaus.groovy:groovy-all:2.4.4'
    testCompile 'org.spockframework:spock-core:0.7-groovy-2.0'
}